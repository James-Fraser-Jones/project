# This is successfully parsed at: http://www.icosaedro.it/bnf_chk/bnf_chk-on-line.html
# Curly {} braces correspond to "many", backslash is escaped, alternative lam and dep symbols are commented out, commas are ommited

#----------------------------Primitives-------------------------------------
bool = "True" | "False" ;

lower = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
      | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
      | "u" | "v" | "w" | "x" | "y" | "z" ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

nat = digit { digit } ;

lam = "λ" | "\\" | "Lam" ;

dep = "Π" | "^" | "Dep" ;

star = "★" | "*" | "Star" ;

box = "☐" | "[]" | "Box" ;

#----------------------------Expressions-------------------------------------
term_lit = bool | nat ;

type_lit = "Bool" | "Nat" ;

lit = box | star | type_lit | term_lit ;

var = lower { lower | digit } | "_" ;

expr_no_app = "(" expr ")"
            | lam var ":" expr "." expr
            | dep var ":" expr "." expr
            | var
            | lit ;

expr = expr_no_app { "@" expr_no_app } ;
